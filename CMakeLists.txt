cmake_minimum_required(VERSION 3.8)

project(xray-1.7.0)

if (CMAKE_SIZEOF_VOID_P EQUAL 4)
    set(ARCH_X86 TRUE)
else()
    set(ARCH_X86 FALSE)
endif()

set(GAME_PATH "" CACHE STRING "path to the game folder, used as output directoy for the engine binaries")
option(BUILD_UTILS "enable utils projects build" OFF)
option(REL_DEVELOP "switch RelWithDebInfo configuration into development mode" OFF)
option(BUILD_TESTS "enable tests projects build" OFF)
option(DEBUG_CAPS "enable debug capabilities" OFF)

if ("${GAME_PATH}" STREQUAL "")
    message(FATAL_ERROR "Invalid game path")
endif()

configure_file(code/engine/xr_config.h.in ${CMAKE_CURRENT_BINARY_DIR}/generated/xr_config.h @ONLY)

if (MSVC)
    if(MSVC_VERSION EQUAL 1911) # MSVS 2017.3
        execute_process(COMMAND "vswhere" "-version" "15.3" "-property" "installationPath"
                        WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/extras
                        OUTPUT_VARIABLE VS_TOOLS_PATH
                        OUTPUT_STRIP_TRAILING_WHITESPACE)
        set(VC_ENV_BAT ${VS_TOOLS_PATH}/VC/Auxiliary/Build/vcvarsall.bat)
        add_compile_options(/std:c++latest /await)
        add_compile_options("$<$<CONFIG:RELWITHDEBINFO>:/Oi>")
    else()
        message(FATAL_ERROR "Only MSVC 15.3 (MSVS 2017) and higher is supported")
    endif()
else()
    message(FATAL_ERROR "Only MSVC compiler is supported")
endif()

set(CMAKE_CONFIGURATION_TYPES "Debug;RelWithDebInfo" CACHE STRING "" FORCE)
set_property(GLOBAL PROPERTY USE_FOLDERS ON)

string(REGEX REPLACE "/EH[a-z]+" "" CMAKE_CXX_FLAGS ${CMAKE_CXX_FLAGS})
add_compile_options(/MP "$<$<CONFIG:RELWITHDEBINFO>:/Ob2>" "$<$<CONFIG:DEBUG>:/EHsc>"
                    "$<$<CONFIG:RELWITHDEBINFO>:/Ot>" "$<$<CONFIG:RELWITHDEBINFO>:/wd4577>")
if (NOT REL_DEVELOP)
    add_compile_options("$<$<CONFIG:RELWITHDEBINFO>:/GL>")
endif()

if (ARCH_X86)
    add_compile_options("$<$<CONFIG:RELWITHDEBINFO>:/arch:SSE2>")
endif()

set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -DDEBUG")
set(CMAKE_CXX_FLAGS_RELWITHDEBINFO "${CMAKE_CXX_FLAGS_RELWITHDEBINFO} -D_HAS_EXCEPTIONS=0")

if (ARCH_X86)
    set(CMAKE_SHARED_LINKER_FLAGS "${CMAKE_SHARED_LINKER_FLAGS} /LARGEADDRESSAWARE")
    set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} /LARGEADDRESSAWARE")
endif()

if (REL_DEVELOP)
    set(SHARED_RELEASE_LINKER_FLAGS "")
else()
    set(SHARED_RELEASE_LINKER_FLAGS "/LTCG /OPT:REF /INCREMENTAL:NO")
endif()
set(STATIC_RELEASE_LINKER_FLAGS "/LTCG")
set(CMAKE_SHARED_LINKER_FLAGS_RELWITHDEBINFO "${CMAKE_SHARED_LINKER_FLAGS_RELWITHDEBINFO} ${SHARED_RELEASE_LINKER_FLAGS}")
set(CMAKE_EXE_LINKER_FLAGS_RELWITHDEBINFO "${CMAKE_EXE_LINKER_FLAGS_RELWITHDEBINFO} ${SHARED_RELEASE_LINKER_FLAGS}")
set(CMAKE_STATIC_LINKER_FLAGS_RELWITHDEBINFO "${CMAKE_STATIC_LINKER_FLAGS_RELWITHDEBINFO} ${STATIC_RELEASE_LINKER_FLAGS}")
unset(SHARED_RELEASE_LINKER_FLAGS)
unset(STATIC_RELEASE_LINKER_FLAGS)

if (ARCH_X86)
    set(ARCH_SUFFIX x86)
else()
    set(ARCH_SUFFIX x64)
endif()

set(BIN_OUT_PATH ${GAME_PATH}/bin/${ARCH_SUFFIX})
set(PDB_OUT_PATH ${GAME_PATH}/pdb/${ARCH_SUFFIX})
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${BIN_OUT_PATH})
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${BIN_OUT_PATH})
set(CMAKE_PDB_OUTPUT_DIRECTORY ${PDB_OUT_PATH})

include_directories(code/3rd-party code/engine code/utils ${CMAKE_CURRENT_BINARY_DIR}/generated)

# 3d-party
if (BUILD_TESTS)
    set(gtest_force_shared_crt ON)
    add_subdirectory(code/3rd-party/gtest)
endif()
add_subdirectory(code/3rd-party/dxsdk)
add_subdirectory(code/3rd-party/zlib)
add_subdirectory(code/3rd-party/minizip)
add_subdirectory(code/3rd-party/dxerr2015)
add_subdirectory(code/3rd-party/ode)
add_subdirectory(code/3rd-party/openal)
add_subdirectory(code/3rd-party/luajit)
add_subdirectory(code/3rd-party/luabind)
add_subdirectory(code/3rd-party/imdexlib)
add_subdirectory(code/3rd-party/libogg)
add_subdirectory(code/3rd-party/libtheora)
add_subdirectory(code/3rd-party/libvorbis)
add_subdirectory(code/3rd-party/libvorbisfile)
add_subdirectory(code/3rd-party/ati)
add_subdirectory(code/3rd-party/loki)
add_subdirectory(code/3rd-party/nvapi)
add_subdirectory(code/3rd-party/DPlay)
add_subdirectory(code/3rd-party/eax)
add_subdirectory(code/3rd-party/cs)
add_subdirectory(code/3rd-party/fmtlib EXCLUDE_FROM_ALL)
add_subdirectory(code/3rd-party/boost)

if (BUILD_TESTS)
    set_target_properties(gmock PROPERTIES FOLDER "3rd-party/gtest")
    set_target_properties(gmock_main PROPERTIES FOLDER "3rd-party/gtest")
    set_target_properties(gtest PROPERTIES FOLDER "3rd-party/gtest")
    set_target_properties(gtest_main PROPERTIES FOLDER "3rd-party/gtest")
endif()
set_target_properties(zlib PROPERTIES FOLDER 3rd-party)
set_target_properties(minizip PROPERTIES FOLDER 3rd-party)
set_target_properties(dxerr2015 PROPERTIES FOLDER 3rd-party)
set_target_properties(ode PROPERTIES FOLDER 3rd-party)
set_target_properties(openal32 PROPERTIES FOLDER 3rd-party)
set_target_properties(luajit-build PROPERTIES FOLDER 3rd-party)
set_target_properties(luabind PROPERTIES FOLDER 3rd-party)
set_target_properties(libogg PROPERTIES FOLDER 3rd-party)
set_target_properties(libtheora PROPERTIES FOLDER 3rd-party)
set_target_properties(libvorbis PROPERTIES FOLDER 3rd-party)
set_target_properties(libvorbisfile PROPERTIES FOLDER 3rd-party)
set_target_properties(ati-src PROPERTIES FOLDER 3rd-party)
set_target_properties(loki-src PROPERTIES FOLDER 3rd-party)
set_target_properties(nvapi-src PROPERTIES FOLDER 3rd-party)
set_target_properties(DPlay-src PROPERTIES FOLDER 3rd-party)
set_target_properties(eax-src PROPERTIES FOLDER 3rd-party)
set_target_properties(cs-src PROPERTIES FOLDER 3rd-party)

#engine
add_subdirectory(code/engine/xrCore)
add_subdirectory(code/engine/xrAPI)
add_subdirectory(code/engine/xrCDB)
add_subdirectory(code/engine/xrXMLParser)
add_subdirectory(code/engine/xrSound)
add_subdirectory(code/engine/xrNetServer)
add_subdirectory(code/engine/xrEngine)
add_subdirectory(code/engine/xrCPU_Pipe)
add_subdirectory(code/engine/xrParticles)
add_subdirectory(code/engine/xrPhysics)
add_subdirectory(code/engine/xrGame)
add_subdirectory(code/engine/xrRenderPC_R1)
add_subdirectory(code/engine/xrRenderPC_R2)
add_subdirectory(code/engine/xrRenderPC_R3)
add_subdirectory(code/engine/xrRenderPC_R4)

set_target_properties(xrCore PROPERTIES FOLDER engine)
set_target_properties(xrAPI PROPERTIES FOLDER engine)
set_target_properties(xrCDB PROPERTIES FOLDER engine)
set_target_properties(xrXMLParser PROPERTIES FOLDER engine)
set_target_properties(xrSound PROPERTIES FOLDER engine)
set_target_properties(xrNetServer PROPERTIES FOLDER engine)
set_target_properties(xrEngine PROPERTIES FOLDER engine)
set_target_properties(xrCPU_Pipe PROPERTIES FOLDER engine)
set_target_properties(xrParticles PROPERTIES FOLDER engine)
set_target_properties(xrPhysics PROPERTIES FOLDER engine)
set_target_properties(xrGame PROPERTIES FOLDER engine)
set_target_properties(xrRender_R1 PROPERTIES FOLDER engine)
set_target_properties(xrRender_R2 PROPERTIES FOLDER engine)
set_target_properties(xrRender_R3 PROPERTIES FOLDER engine)
set_target_properties(xrRender_R4 PROPERTIES FOLDER engine)

if (MSVC)
    set_property(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT xrEngine)
endif()

if (${BUILD_UTILS})
    add_subdirectory(code/3rd-party/freeimage/libjpeg)
    add_subdirectory(code/3rd-party/freeimage/libjxr)
    add_subdirectory(code/3rd-party/freeimage/libopenjpeg)
    add_subdirectory(code/3rd-party/freeimage/libpng)
    add_subdirectory(code/3rd-party/freeimage/librawlite)
    add_subdirectory(code/3rd-party/freeimage/libtiff4)
    add_subdirectory(code/3rd-party/freeimage/libwebp)
    add_subdirectory(code/3rd-party/freeimage/openexr)
    add_subdirectory(code/3rd-party/freeimage/freeimage)

    set_target_properties(libjpeg PROPERTIES FOLDER "3rd-party/freeimage")
    set_target_properties(libjxr PROPERTIES FOLDER "3rd-party/freeimage")
    set_target_properties(libopenjpeg PROPERTIES FOLDER "3rd-party/freeimage")
    set_target_properties(libpng PROPERTIES FOLDER "3rd-party/freeimage")
    set_target_properties(libraw PROPERTIES FOLDER "3rd-party/freeimage")
    set_target_properties(libtiff4 PROPERTIES FOLDER "3rd-party/freeimage")
    set_target_properties(libwebp PROPERTIES FOLDER "3rd-party/freeimage")
    set_target_properties(openexr PROPERTIES FOLDER "3rd-party/freeimage")
    set_target_properties(FreeImage PROPERTIES FOLDER "3rd-party/freeimage")

    add_subdirectory(code/utils/ctool)
    add_subdirectory(code/utils/xrCompress)
    add_subdirectory(code/utils/xrQSlim)
    add_subdirectory(code/utils/xrSE_Factory)
    add_subdirectory(code/utils/ETools)
    add_subdirectory(code/utils/CompressionTest)
    add_subdirectory(code/utils/LWO)
    add_subdirectory(code/utils/xrAI)
    add_subdirectory(code/utils/xrDXT)
    add_subdirectory(code/utils/xrLC_Light)
    add_subdirectory(code/utils/xrLC_LightStab)
    add_subdirectory(code/utils/xrDO_Light)
    add_subdirectory(code/utils/xrLC)

    set_target_properties(ctool PROPERTIES FOLDER utils)
    set_target_properties(xrCompress PROPERTIES FOLDER utils)
    set_target_properties(xrQSlim PROPERTIES FOLDER utils)
    set_target_properties(xrSE_Factory PROPERTIES FOLDER utils)
    set_target_properties(ETools PROPERTIES FOLDER utils)
    set_target_properties(CompressionTest PROPERTIES FOLDER utils)
    set_target_properties(LWO PROPERTIES FOLDER utils)
    set_target_properties(xrAI PROPERTIES FOLDER utils)
    set_target_properties(xrDXT PROPERTIES FOLDER utils)
    set_target_properties(xrLC_Light PROPERTIES FOLDER utils)
    set_target_properties(xrLC_LightStab PROPERTIES FOLDER utils)
    set_target_properties(xrDO_Light PROPERTIES FOLDER utils)
    set_target_properties(xrLC PROPERTIES FOLDER utils)
endif()